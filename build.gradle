plugins {
  id 'application'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.3.9.RELEASE'
  id 'org.owasp.dependencycheck' version '6.1.2'
  id 'com.github.ben-manes.versions' version '0.36.0'
  id 'org.sonarqube' version '3.1.1'
}

group = 'uk.gov.hmcts.ethos.ecm'
version = '0.0.1'

allprojects {
  sourceCompatibility = '11.0'
  targetCompatibility = '11.0'
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

def sonarExclusions = ['**/uk/gov/hmcts/reform/ethos/ecm/consumer/config/**',
                       '**/uk/gov/hmcts/reform/ethos/ecm/consumer/model/**',
                       '**/uk/gov/hmcts/reform/ethos/ecm/consumer/Application'
]

sonarqube {
  properties {
    property "sonar.exclusions", sonarExclusions.join(', ')
    property "sonar.projectKey", "ecm-consumer"
    property "sonar.dynamicAnalysis", "reuseReports"
    property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sourceSets {
  test {
    java.srcDir 'src/test/java'
  }
  functional {
    java.srcDir 'src/test/functional/java'
    resources.srcDir 'src/test/functional/resources'
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

configurations {
  functionalCompile.extendsFrom testCompile
  functionalRuntime.extendsFrom testRuntime
}

test {
  exclude '**/*ComponentTest*'
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath

  useJUnit {
    includeCategories 'uk.gov.hmcts.ethos.ecm.consumer.functional.SmokeTest'
  }
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath

  useJUnit {
    includeCategories 'uk.gov.hmcts.ethos.ecm.consumer.functional.SmokeTest'
  }

  maxHeapSize = '1G'
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.68'
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '30.0-jre') {
      entry 'guava'
    }
  }
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
  maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
  maven { url "https://repository.apache.org/content/repositories/snapshots"}
}

def versions = [
  junitPlatform   : '1.6.2',
  springBoot      : springBoot.class.package.implementationVersion,
  springfoxSwagger: '2.9.2',
  logging         : '5.1.7',
  hmctsNotify     : '3.15.1-RELEASE',
  serenity        : '2.3.31'
]

dependencies {

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-configuration-processor'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.retry:spring-retry'

  implementation 'org.postgresql:postgresql:42.2.2'

  implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger
  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.7'
  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '2.2.5.RELEASE'
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '11.1'
  implementation group: 'io.github.openfeign', name: 'feign-core', version: '11.1'

  implementation group: 'uk.gov.hmcts.reform', name: 'ecm-common', version: '0.1.53'
  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: versions.hmctsNotify
  implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.logging
  implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.logging
  implementation group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.1.0'

  implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '4.22.1'

  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'


  testCompile 'org.springframework.boot:spring-boot-starter-test'

  testCompile (group: 'org.docx4j', name: 'docx4j', version: '6.1.2') {
    exclude module: 'slf4j-log4j12'
  }
  testCompile group: 'com.auth0', name: 'java-jwt', version: '3.11.0'
  testCompile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
  testCompile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
  testCompile group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'

  testCompile group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
  testCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity, {
    exclude module: 'jaxb-osgi'
  }
  testCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
  testCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
}

mainClassName = 'uk.gov.hmcts.reform.ethos.ecm.consumer.Application'

bootJar {
  getArchiveFileName().set(provider {
    'ecm-consumer.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
