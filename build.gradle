plugins {
  id 'application'
  id 'jacoco'
  id 'pmd'
  id 'checkstyle'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.3.11.RELEASE'
  id 'org.owasp.dependencycheck' version '6.2.2'
  id 'com.github.ben-manes.versions' version '0.39.0'
  id 'org.sonarqube' version '3.3'
}

group = 'uk.gov.hmcts.ethos.ecm'
version = '0.0.1'

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
}

allprojects {
  sourceCompatibility = '11.0'
  targetCompatibility = '11.0'
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

def sonarExclusions = ['**/uk/gov/hmcts/reform/ethos/ecm/consumer/config/**',
                       '**/uk/gov/hmcts/reform/ethos/ecm/consumer/model/**',
                       '**/uk/gov/hmcts/reform/ethos/ecm/consumer/domain/**',
                       '**/uk/gov/hmcts/reform/ethos/ecm/consumer/tasks/**',
                       '**/uk/gov/hmcts/reform/ethos/ecm/consumer/Application'
]

sonarqube {
  properties {
    property "sonar.exclusions", sonarExclusions.join(', ')
    property "sonar.projectKey", "ecm-consumer"
    property "sonar.dynamicAnalysis", "reuseReports"
    property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sourceSets {
  test {
    java.srcDir 'src/test/java'
  }
  functional {
    java.srcDir 'src/test/functional/java'
    resources.srcDir 'src/test/functional/resources'
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

configurations {
  functionalImplementation.extendsFrom testImplementation
  functionalRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
  exclude '**/*ComponentTest*'
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath

  useJUnit {
    includeCategories 'uk.gov.hmcts.ethos.ecm.consumer.functional.SmokeTest'
  }
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath

  useJUnit {
    includeCategories 'uk.gov.hmcts.ethos.ecm.consumer.functional.SmokeTest'
    includeCategories 'uk.gov.hmcts.ethos.ecm.consumer.functional.HealthCheckTest'
  }

  maxHeapSize = '1G'
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.68'
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '30.1.1-jre') {
      entry 'guava'
    }
  }
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
  maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
  maven { url "https://repository.apache.org/content/repositories/snapshots"}
  maven { url "https://jitpack.io" }
}

def versions = [
  junitPlatform   : '1.6.2',
  springBoot      : springBoot.class.package.implementationVersion,
  springfoxSwagger: '2.9.2',
  logging         : '5.1.7',
  hmctsNotify     : '3.15.1-RELEASE',
  serenity        : '2.3.31'
]

dependencies {
  implementation 'com.github.hmcts:ecm-common:0.1.103'

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-configuration-processor'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.retry:spring-retry'

  implementation 'org.postgresql:postgresql:42.2.22'

  implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger
  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.10.1'
  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '2.2.5.RELEASE'
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '11.4'
  implementation group: 'io.github.openfeign', name: 'feign-core', version: '11.1'

  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: versions.hmctsNotify
  implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.logging
  implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.logging
  implementation group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.1.0'

  implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.7'
  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '4.24.0'
  implementation 'org.testng:testng:7.1.0'

  implementation group: 'junit', name: 'junit', version: '4.13.1'

  implementation 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  // For pdfbox and elasticsearch CVEs
  implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.24'
  implementation('org.elasticsearch:elasticsearch') {
    version {
      strictly '7.13.2'
    }
  }

  testImplementation 'org.springframework.boot:spring-boot-starter-test'

  testImplementation (group: 'org.docx4j', name: 'docx4j', version: '6.1.2') {
    exclude module: 'slf4j-log4j12'
  }
  testImplementation group: 'com.auth0', name: 'java-jwt', version: '3.14.0'
  testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
  testImplementation group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
  testImplementation group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'

  testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity, {
    exclude module: 'jaxb-osgi'
  }
  testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
}

mainClassName = 'uk.gov.hmcts.reform.ethos.ecm.consumer.Application'

bootJar {
  getArchiveFileName().set(provider {
    'ecm-consumer.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
